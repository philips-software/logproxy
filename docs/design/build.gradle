// This buildscript is a result of several issues around asciidoctor
//
// https://github.com/asciidoctor/asciidoctor-pdf/issues/271
// https://github.com/junit-team/junit5/pull/779
// https://github.com/junit-team/junit5/pull/784
// https://github.com/mojavelinux/junit5/blob/a1fd3d9d8b52e2c68f669d3d5daf6d242e68ed3e/documentation/documentation.gradle

import static groovy.io.FileType.*
import java.security.MessageDigest

buildscript {
    repositories {
        jcenter()
    }

    dependencies {
        classpath 'org.asciidoctor:asciidoctorj-pdf:1.5.0-alpha.16'
        classpath 'org.asciidoctor:asciidoctorj-diagram:1.5.12'
    }
}

plugins {
    id 'org.asciidoctor.convert' version '1.6.0'
}

asciidoctor {
    requires 'asciidoctor-diagram'

    separateOutputDirs false
    sources { include 'index.adoc' }
    backends 'html5', 'pdf' // , 'epub3'

    attributes \
        'outdir': outputDir.absolutePath

    asciidoctorj {
        attributes revnumber: '1.0'
    }
}

ext {
    targetDir = project.buildDir
}

task collectIncludes(
        description: 'collect all ADRs as includes in one file',
        group: 'docToolchain'
) {
    doFirst {
        new File(targetDir, '_includes').mkdirs()
        def adr_includes_file = new File(targetDir, '_includes/ADR_includes.adoc')
        if( !adr_includes_file.exists() ) {
            adr_includes_file.write("\n")
        }
    }
    doLast {
        //let's search the whole project for files, not only the docs folder
        //could be a problem with node projects :-)
        File sourceFolder = projectDir
        println "sourceFolder: " + sourceFolder.canonicalPath
        def collections = [:]
        sourceFolder.traverse(type: FILES) { file ->
            if (file.name ==~ '^[A-Z]{3}-.*[.](ad|adoc|asciidoc)$') {
                def type = file.name[0..2]
                if (!collections[type]) {
                    collections[type] = []
                }
                collections[type] << file.canonicalPath
            }
        }
        collections.each { type, fileNames ->
            def outFile = new File(targetDir.path+'/_includes', type+'_includes.adoc')

            outFile.write("// this is autogenerated\n")
            fileNames.sort(false).each { fileName ->
                println "now processing fileName: " + fileName

                outFile.append( "include::" + fileName.replace("\\", "/") + "[leveloffset=+1]\n\n" )
            }
        }
    }
}

asciidoctor.dependsOn collectIncludes